10 REM *** BasiCrash v. 0.06b ***
15 REM GNU General Public License v3.0 or later
20 DEFINT A-Z
30 KEY 1,"SCORE:":KEY3,"":KEY4,"":KEY5,""
100 SCREEN 0,,0:KEY 2,STR$(RE)
110 FOR Y=1 TO 22
115 T$=INKEY$
120 FOR X=0 TO 39
130 VPOKE X+Y*40,0
140 NEXT X
150 NEXT Y
160 FOR X=1 TO 30
170 A=INT(RND(1)*38)+1:B=INT(RND(1)*22)+1
180 IF VPEEK(A+B*40)<>0 OR (VPEEK(A+1+B*40)=42 AND (VPEEK(A-1+B*40)=42 OR VPEEK(A+(B-1)*40)=42 OR VPEEK(A+1+(B-1)*40)=42)) GOTO 170 ELSE VPOKE A+B*40,111
190 A=INT(RND(1)*37)+2:B=INT(RND(1)*22)+1
200 IF VPEEK(A+B*40)<>0 OR (VPEEK(A-1+B*40)=111 AND (VPEEK(A-2+B*40)=42 OR VPEEK(A-1+(B-1)*40)=42 OR VPEEK(A+(B-1)*40)=42)) GOTO 190
210 IF B=22 AND VPEEK(A+(B-1)*40)=42 GOTO 190
220 IF B=21 THEN IF VPEEK(A+(B+1)*40)=42 GOTO 190
230 IF B<22 THEN IF (VPEEK(A+(B-1)*40)=42 AND VPEEK(A+(B+1)*40)=42) GOTO 190
240 IF B<21 THEN IF (VPEEK(A+(B+1)*40)=42 AND VPEEK(A+(B+2)*40)=42) GOTO 190
250 IF B>1 THEN IF (VPEEK(A+(B-1)*40)=42 AND VPEEK(A+(B-2)*40)=42) GOTO 190
260 VPOKE A+B*40,42
270 NEXT X
350 F=0:N=0:P=0
400 VPOKE F,0:VPOKE N,62
410 F=N
420 FOR X=1 TO 80:NEXT X
430 T$=INKEY$
440 IF T$="" GOTO 470
450 IF T$=CHR$(31) AND N<880 THEN N=N+40
460 IF T$=CHR$(30) AND N>39 THEN N=N-40
470 N=N+1
480 IF N>919 THEN N=880
490 IF VPEEK(N)=111 THEN P=P+1 ELSE 510
500 KEY 2,STR$(RE+P):IF P=30 THEN RE=RE+30:GOTO 100
510 IF VPEEK(N)<>42 GOTO 400
520 RE=0:P=0
530 PRINT " GAME OVER "
540 PRINT
550 PRINT " Press ENTER to restart "
560 T$=INPUT$(1)
570 IF T$=CHR$(13) GOTO 100 ELSE 560

